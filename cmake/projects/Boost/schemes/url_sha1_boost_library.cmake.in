# Copyright (c) 2013-2015, Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 3.0)
project(Hunter)

include(ExternalProject) # ExternalProject_Add

# Scheme for download and install boost library

list(APPEND CMAKE_MODULE_PATH "@HUNTER_SELF@/cmake/modules")

include(CheckSymbolExists) # check_symbol_exists

include(hunter_boost_component_b2_args)
include(hunter_dump_cmake_flags)
include(hunter_fatal_error)
include(hunter_get_boost_libs)
include(hunter_install_boost_config)
include(hunter_internal_error)
include(hunter_pick_archiver)
include(hunter_status_debug)
include(hunter_status_print)
include(hunter_test_string_not_empty)
include(hunter_user_error)
include(hunter_parse_boost_config_macros)

hunter_status_debug("Scheme: url_sha1_boost_library")

# Check preconditions
hunter_test_string_not_empty("@HUNTER_SELF@")
hunter_test_string_not_empty("@HUNTER_EP_NAME@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_DOWNLOAD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SOURCE_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_BUILD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_INSTALL_PREFIX@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_COMPONENT@")
hunter_test_string_not_empty("@CMAKE_CXX_COMPILER@")
hunter_test_string_not_empty("@CMAKE_CXX_COMPILER_ID@")
hunter_test_string_not_empty("@HUNTER_Boost_VERSION@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SCRIPT_DIR@")

if("@MSVC@")
  hunter_test_string_not_empty("@HUNTER_MSVC_ARCH@")
  hunter_test_string_not_empty("@HUNTER_MSVC_VCVARSALL@")

  set(x "$ENV{USERPROFILE}/source")
  if(EXISTS "${x}")
    hunter_user_error(
        "Please remove directory '${x}' (see https://github.com/ruslo/hunter/issues/745#issuecomment-316629752)"
    )
  endif()
endif()

# get list of boost components for given version
hunter_get_boost_libs(VERSION "@HUNTER_Boost_VERSION@" LIBS boost_libs)

set(libfound NO)
foreach(x ${boost_libs})
  string(COMPARE EQUAL "${x}" "@HUNTER_PACKAGE_COMPONENT@" result)
  if(result)
    set(libfound YES)
  endif()
endforeach()

if(NOT libfound)
  hunter_internal_error("No such library")
endif()

string(COMPARE EQUAL "@CMAKE_CXX_COMPILER_ID@" "Clang" compiler_is_clang)

set(use_cmake_archiver TRUE)
if("@ANDROID@")
  set(toolset_name "gcc")
  set(toolset_version "ndk")
elseif("@APPLE@")
  set(toolset_name "darwin")
  set(toolset_version "")

  # Using CMAKE_AR on OSX leads to error (b2 use 'libtool'):
  # * https://travis-ci.org/ingenue/hunter/jobs/204617507
  set(use_cmake_archiver FALSE)
elseif("@CMAKE_COMPILER_IS_GNUCXX@")
  set(toolset_name "gcc")
  set(toolset_version "")
elseif(compiler_is_clang)
  set(toolset_name "clang")
  set(toolset_version "")
elseif("@MSVC@")
  set(toolset_name "msvc")
  set(toolset_version "")
elseif("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Intel")
  set(toolset_name "intel")
  set(toolset_version "")
else()
  hunter_fatal_error("TODO: set toolset for boost" WIKI "error.boost.toolset")
endif()

hunter_dump_cmake_flags()
# -> CMAKE_C_FLAGS
# -> CMAKE_CXX_FLAGS

if("@ANDROID@")
  get_directory_property(defs COMPILE_DEFINITIONS)
  foreach(def ${defs})
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -D${def}")
  endforeach()

  get_directory_property(include_dirs INCLUDE_DIRECTORIES)
  foreach(include_dir ${include_dirs})
    set(
        CMAKE_CXX_FLAGS
        "${CMAKE_CXX_FLAGS} ${CMAKE_INCLUDE_SYSTEM_FLAG_CXX} ${include_dir}"
    )
  endforeach()
endif()

if("@MSVC@")
  set(boost_compiler  "@CMAKE_CXX_COMPILER@")
  string(REPLACE "/" "\\" boost_compiler "${boost_compiler}")
else()
  set(boost_compiler "@CMAKE_CXX_COMPILER@")
endif()

string(COMPARE EQUAL "@HUNTER_PACKAGE_COMPONENT@" "mpi" is_mpi)
if(is_mpi)
  set(using_mpi "using mpi ;")
  if("@MSVC@")
    find_package(MPI)
    if(NOT MPI_CXX_FOUND)
      # Fix concurrent output to console
      execute_process(
          COMMAND
          "@CMAKE_COMMAND@"
          -E
          echo
          "MPI is required. Please install it. For example MS-MPI: "
          "http://msdn.microsoft.com/en-us/library/bb524831%28v=vs.85%29.aspx"
      )
      hunter_fatal_error("MPI is required" WIKI "error.boost.mpi.on.windows")
    endif()
    get_filename_component(MPI_DIR "${MPI_CXX_INCLUDE_PATH}/.." ABSOLUTE)
    string(REPLACE "/" "\\\\" MPI_DIR "${MPI_DIR}")
    configure_file(
        "@HUNTER_PACKAGE_SCRIPT_DIR@/mpi.jam"
        "@HUNTER_PACKAGE_BUILD_DIR@/tools/build/v2/tools/mpi.jam"
    )

    # HUNTER_PACKAGE_SOURCE_DIR will be removed while unpacking. Save mpi.jam
    # in HUNTER_PACKAGE_BUILD_DIR first and move it to HUNTER_PACKAGE_SOURCE_DIR
    # on configure step
    set(
        copy_mpi_command
        COMMAND
        "@CMAKE_COMMAND@"
        -E
        copy
        "@HUNTER_PACKAGE_BUILD_DIR@/tools/build/v2/tools/mpi.jam"
        "@HUNTER_PACKAGE_SOURCE_DIR@/tools/build/v2/tools/mpi.jam"
    )
  endif()
  set(variants variant=release) # build bug
else()
  set(using_mpi "")
  set(variants variant=debug,release)
  set(copy_mpi_command "")
endif()

set(BOOST_ALL_DYN_LINK "")
if(BUILD_SHARED_LIBS) #if using shared libs overide boost config
  set(BOOST_BUILD_STATIC OFF)
  if("@MSVC@")
    set(BOOST_ALL_DYN_LINK ON)
  endif()
else()
  set(BOOST_BUILD_STATIC ON)
endif()

if("@MSVC@")
  # Disable auto-linking
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /DBOOST_ALL_NO_LIB=1")

  # Fix some compile errors
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /DNOMINMAX")

  # Fix boost.python:
  # include\pymath.h: warning C4273: 'round': inconsistent dll linkage
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /DHAVE_ROUND")
endif()

string(COMPARE NOTEQUAL "@CMAKE_OSX_SYSROOT@" "" have_osx_sysroot)
if(have_osx_sysroot)
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -isysroot @CMAKE_OSX_SYSROOT@")
endif()

if(BOOST_BUILD_STATIC)
  set(link_opts link=static)
  if("@MSVC@" AND NOT BOOST_BUILD_DYNAMIC_VSRUNTIME)
    list(APPEND link_opts "runtime-link=static")
  endif()
else()
  set(link_opts link=shared)
endif()

set(toolset_full_name ${toolset_name})
string(COMPARE NOTEQUAL "${toolset_version}" "" has_toolset_version)
if(has_toolset_version)
  set(toolset_full_name ${toolset_name}-${toolset_version})
endif()

set(boost_user_jam "@HUNTER_PACKAGE_BUILD_DIR@/boost.user.jam")
set(
    build_opts
    -a
    ${link_opts}
    threading=multi
    ${variants}
    --layout=tagged
    toolset=${toolset_full_name}
    "--user-config=${boost_user_jam}"
    --with-@HUNTER_PACKAGE_COMPONENT@
)
if("@MINGW@")
  # cross compile from "linux" to "windows" using mingw
  set(build_opts target-os=windows ${build_opts})
endif()

hunter_boost_component_b2_args(
    "@HUNTER_PACKAGE_COMPONENT@"
    "@HUNTER_Boost_CMAKE_ARGS@"
    b2_component_opts
)

string(REPLACE " " ";" cxx_flags_list "${CMAKE_CXX_FLAGS}")

file(
    WRITE
    ${boost_user_jam}
    "using ${toolset_name}\n"
    "  : ${toolset_version}\n"
)

if(MSVC)
  # For Visual Studio C++ flags must not be set in compiler section.
  # Section <compileflags> should be used.
  #   * https://github.com/ruslo/hunter/issues/179
  file(APPEND ${boost_user_jam} "  : \"${boost_compiler}\"\n")
else()
  # For Android C++ flags must be part of the compiler section:
  #   * https://github.com/ruslo/hunter/issues/174
  # For 'sanitize-address' toolchain flags must be part of the compiler section:
  #   * https://github.com/ruslo/hunter/issues/269
  file(
      APPEND ${boost_user_jam} "  : \"${boost_compiler}\" ${CMAKE_CXX_FLAGS}\n"
  )
endif()

if(use_cmake_archiver)
  # We need custom '<archiver>' and '<ranlib>' for
  # Android LTO ('*-gcc-ar' instead of '*-ar')
  # WARNING: no spaces between '<archiver>' and '${CMAKE_AR}'!

  # -> CMAKE_AR
  # -> CMAKE_RANLIB
  hunter_pick_archiver()

  file(
      APPEND
      ${boost_user_jam}
      "  : <archiver>\"${CMAKE_AR}\"\n"
      " <ranlib>\"${CMAKE_RANLIB}\"\n"
  )
endif()

if(MSVC)
  # See 'boost_compiler' section
  foreach(cxx_flag ${cxx_flags_list})
    file(
        APPEND
        ${boost_user_jam}
        "  <compileflags>${cxx_flag}\n"
    )
  endforeach()
endif()

file(
    APPEND
    ${boost_user_jam}
    ";\n"
    "${using_mpi}\n"
)

list(APPEND build_opts ${b2_component_opts})

if(CMAKE_EXE_LINKER_FLAGS)
  list(APPEND build_opts "linkflags=${CMAKE_EXE_LINKER_FLAGS}")
endif()

string(COMPARE EQUAL "@HUNTER_MSVC_ARCH@" "amd64" is_x64)
if("@MSVC@" AND is_x64)
  list(APPEND build_opts "address-model=64")
endif()

string(COMPARE NOTEQUAL "@HUNTER_JOBS_OPTION@" "" have_jobs)
if(have_jobs)
  list(APPEND build_opts "-j" "@HUNTER_JOBS_OPTION@")
endif()

if(@HUNTER_STATUS_DEBUG@)
  set(verbose_output "-d+2 --debug-configuration")
endif()

if(CMAKE_HOST_WIN32)
  set(b2_cmd "b2")
  if("@MSVC@")
    file(WRITE "@HUNTER_PACKAGE_DOWNLOAD_DIR@/patched_boostrap.bat" "\
call \"@HUNTER_MSVC_VCVARSALL@\" @HUNTER_MSVC_ARCH@\n\
cd @HUNTER_PACKAGE_SOURCE_DIR@\n\
bootstrap.bat")
    set(patch_cmd "@CMAKE_COMMAND@" -E copy "@HUNTER_PACKAGE_DOWNLOAD_DIR@/patched_boostrap.bat" "@HUNTER_PACKAGE_SOURCE_DIR@/patched_boostrap.bat")
    set(bootstrap_cmd "patched_boostrap.bat")
  else()
    set(patch_cmd "")
    set(bootstrap_cmd "bootstrap.bat")
  endif()
else()
  set(b2_cmd "./b2")
  set(bootstrap_cmd "./bootstrap.sh")
  set(patch_cmd "")
endif()

if("@MINGW@")
  list(APPEND bootstrap_cmd "gcc")
endif()

if(HUNTER_STATUS_DEBUG)
  file(READ "${boost_user_jam}" USER_JAM_CONTENT)
  hunter_status_debug("Build options:")
  foreach(opt ${build_opts})
    hunter_status_debug("  ${opt}")
  endforeach()
  hunter_status_debug("------")
  hunter_status_debug("Boost user jam config:")
  hunter_status_debug("------")
  hunter_status_debug("${USER_JAM_CONTENT}")
  hunter_status_debug("------")
endif()

if("@MSVC@")
  # Logging as Workaround for VS_UNICODE_OUTPUT issue:
  # https://public.kitware.com/Bug/view.php?id=14266
  set(log_opts LOG_CONFIGURE 1 LOG_BUILD 1 LOG_INSTALL 1 LOG_DOWNLOAD 1)
  get_filename_component(x "@HUNTER_PACKAGE_SOURCE_DIR@/.." ABSOLUTE)
  hunter_status_print(
      "For progress check log files in directory: ${x}"
  )
else()
  set(log_opts "")
endif()

hunter_parse_boost_config_macros(BOOST_CONFIG_MACROS "@HUNTER_Boost_CMAKE_ARGS@")
configure_file(
  "@HUNTER_GLOBAL_SCRIPT_DIR@/append-boost-config-macros.cmake.in"
  "@HUNTER_PACKAGE_BUILD_DIR@/append-boost-config-macros.cmake"
  @ONLY
)

ExternalProject_Add(
    "@HUNTER_EP_NAME@"
    URL
    @HUNTER_PACKAGE_URL@
    URL_HASH
    SHA1=@HUNTER_PACKAGE_SHA1@
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_PACKAGE_INSTALL_PREFIX@"
        # not used, just avoid creating Install/<name> empty directory
    UPDATE_COMMAND
    "@CMAKE_COMMAND@" -P
    "@HUNTER_PACKAGE_BUILD_DIR@/append-boost-config-macros.cmake"
    CONFIGURE_COMMAND
    ${patch_cmd}
    ${copy_mpi_command}
    COMMAND
    ${bootstrap_cmd}
    BUILD_COMMAND
    ${b2_cmd}
    ${verbose_output}
    ${build_opts}
    --ignore-site-config # Ignore Gentoo specific optimization "none" in site config that only the patched bjam of Gentoo can understand.
    BUILD_IN_SOURCE
    1
    INSTALL_COMMAND
    ${b2_cmd}
    -d0
    ${build_opts}
    stage # install only libraries, headers installed in `url_sha1_boost`
    "--stagedir=@HUNTER_PACKAGE_INSTALL_PREFIX@"
    --ignore-site-config # Ignore Gentoo specific optimization "none" in site config that only the patched bjam of Gentoo can understand.
    ${log_opts}
)

# Forward some variables
set(HUNTER_SELF "@HUNTER_SELF@")
set(HUNTER_PACKAGE_INSTALL_PREFIX "@HUNTER_PACKAGE_INSTALL_PREFIX@")
set(HUNTER_Boost_VERSION "@HUNTER_Boost_VERSION@")

hunter_install_boost_config()
