# Copyright (c) 2013, Ruslan Baratov
# All rights reserved.

cmake_minimum_required(VERSION 3.0)

include(ExternalProject) # ExternalProject_Add

# Scheme for download and install ios universal boost library

list(APPEND CMAKE_MODULE_PATH "@HUNTER_SELF@/cmake/modules")

include(hunter_assert_lists_equal)
include(hunter_internal_error)
include(hunter_status_debug)
include(hunter_test_string_not_empty)
include(hunter_unsetvar)

hunter_status_debug("Scheme: url_sha1_boost_ios_library")

# Check preconditions
hunter_test_string_not_empty("@HUNTER_BASE@")
hunter_test_string_not_empty("@HUNTER_SELF@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_BASENAME@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_DOWNLOAD_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_SOURCE_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_INSTALL_DIR@")
hunter_test_string_not_empty("@HUNTER_PACKAGE_COMPONENT@")
hunter_test_string_not_empty("@IPHONEOS_ARCHS@")
hunter_test_string_not_empty("@IPHONESIMULATOR_ARCHS@")
hunter_test_string_not_empty("@IPHONESIMULATOR_ROOT@")
hunter_test_string_not_empty("@IPHONEOS_ROOT@")
hunter_test_string_not_empty("@IPHONEOS_SDK_ROOT@")
hunter_test_string_not_empty("@IPHONESIMULATOR_SDK_ROOT@")
hunter_test_string_not_empty("@HUNTER_Boost_VERSION@")

hunter_assert_lists_equal(@HUNTER_DOWNLOAD_SCHEME_VARIANTS@ ios ios_sim ios_universal)

set_directory_properties(
    PROPERTIES
    EP_BASE
    "@HUNTER_BASE@"
)

set(
    boost_libs
    atomic
    chrono
    context
    coroutine
    date_time
    exception
    filesystem
    graph
    graph_parallel
    iostreams
    locale
    log
    math
    mpi
    program_options
    python
    random
    regex
    serialization
    signals
    system
    test
    thread
    timer
    wave
)

# Same logic in all url_sha1_boost* schemes (TODO: DRY)
if(NOT @HUNTER_Boost_VERSION@ VERSION_LESS "1.56.0")
  # >= 1.56
  list(APPEND boost_libs container)
endif()

set(libfound NO)
foreach(x ${boost_libs})
  string(COMPARE EQUAL "${x}" "@HUNTER_PACKAGE_COMPONENT@" result)
  if(result)
    set(libfound YES)
  endif()
endforeach()

if(NOT libfound)
  hunter_internal_error("No such library")
endif()

hunter_unsetvar(iphoneos_archs)
foreach(x @IPHONEOS_ARCHS@)
  set(iphoneos_archs "${iphoneos_archs} -arch ${x}")
endforeach()

hunter_unsetvar(isim_archs)
foreach(x @IPHONESIMULATOR_ARCHS@)
  set(isim_archs "${isim_archs} -arch ${x}")
endforeach()

set(boost_user_jam @HUNTER_BASE@/boost.user.jam)
file(
     WRITE
     ${boost_user_jam}
     "using darwin : iphoneos\n"
     "    : xcrun clang++ ${iphoneos_archs} -isysroot @IPHONEOS_SDK_ROOT@\n"
     "    : <striper> <root>@IPHONEOS_ROOT@\n"
     "    : <architecture>arm <target-os>iphone\n"
     ";\n"
     "\n"
     "using darwin : iphonesimulator\n"
     "    : xcrun clang++ ${isim_archs} -isysroot @IPHONESIMULATOR_SDK_ROOT@\n"
     "    : <striper> <root>@IPHONESIMULATOR_ROOT@\n"
     "    : <architecture>x86 <target-os>iphone\n"
     ";"
)

set(
    build_opts
    -a
    link=static
    threading=multi
    variant=release,debug
    define=BOOST_SP_NO_SYNC
    --layout=tagged
    --user-config="${boost_user_jam}"
)

if(CMAKE_CXX_FLAGS)
  list(APPEND build_opts "cxxflags=${CMAKE_CXX_FLAGS}")
endif()

set(
    build_opts_iphoneos
    toolset=darwin-iphoneos
    target-os=iphone
    architecture=arm
    instruction-set=armv7
    ${build_opts}
)

set(
    build_opts_iphonesimulator
    toolset=darwin-iphonesimulator
    target-os=iphone
    architecture=x86
    ${build_opts}
)

# Clear Xcode environment
set(
    bootstrap
    . "@HUNTER_SELF@/scripts/clear-all.sh" &&
    ./bootstrap.sh
    "--with-libraries=@HUNTER_PACKAGE_COMPONENT@"
    "--prefix=@HUNTER_PACKAGE_INSTALL_DIR@"
)

if(@HUNTER_STATUS_DEBUG@)
  set(verbose_output "-d+2 --debug-configuration")
endif()

ExternalProject_Add(
    @HUNTER_PACKAGE_BASENAME@-ios
    DOWNLOAD_COMMAND
    ${CMAKE_COMMAND} -E echo "skip download"
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_PACKAGE_INSTALL_DIR@"
        # not used, just avoid creating Install/<name> empty directory
    CONFIGURE_COMMAND
    ${bootstrap}
    BUILD_COMMAND
    ./b2
    ${verbose_output}
    ${build_opts_iphoneos}
    BUILD_IN_SOURCE
    1
    INSTALL_COMMAND
    "${CMAKE_COMMAND}" -E echo "skip install"
)

ExternalProject_Add(
    @HUNTER_PACKAGE_BASENAME@-ios_sim
    DOWNLOAD_COMMAND
    ${CMAKE_COMMAND} -E echo "skip download"
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_PACKAGE_INSTALL_DIR@"
        # not used, just avoid creating Install/<name> empty directory
    CONFIGURE_COMMAND
    ${bootstrap}
    BUILD_COMMAND
    ./b2
    ${verbose_output}
    ${build_opts_iphonesimulator}
    BUILD_IN_SOURCE
    1
    INSTALL_COMMAND
    "${CMAKE_COMMAND}" -E echo "skip install"
)

string(COMPARE EQUAL "@HUNTER_PACKAGE_COMPONENT@" "test" is_test)
if(is_test)
  set(LIBNAME unit_test_framework)
else()
  set(LIBNAME "@HUNTER_PACKAGE_COMPONENT@")
endif()

string(COMPARE EQUAL "@HUNTER_PACKAGE_COMPONENT@" "log" log_setup_target)
if(log_setup_target)
  set(
      extra_lib_command
      COMMAND
      "@HUNTER_SELF@/scripts/create-boost-ios-fatlib.sh"
      "@HUNTER_PACKAGE_SOURCE_DIR@"
      "log_setup"
      "@HUNTER_PACKAGE_COMPONENT@"
      "@HUNTER_PACKAGE_INSTALL_DIR@/lib"
  )
else()
  set(extra_lib_command "")
endif()

ExternalProject_Add(
    @HUNTER_PACKAGE_BASENAME@-ios_universal
    DEPENDS
    @HUNTER_PACKAGE_BASENAME@-ios
    @HUNTER_PACKAGE_BASENAME@-ios_sim
    DOWNLOAD_COMMAND
    ${CMAKE_COMMAND} -E echo "skip download"
    DOWNLOAD_DIR
    "@HUNTER_PACKAGE_DOWNLOAD_DIR@"
    SOURCE_DIR
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    INSTALL_DIR
    "@HUNTER_PACKAGE_INSTALL_DIR@"
        # not used, just avoid creating Install/<name> empty directory
    CONFIGURE_COMMAND
    "${CMAKE_COMMAND}" -E echo "skip configure"
    BUILD_COMMAND
    "${CMAKE_COMMAND}" -E echo "skip build"
    INSTALL_COMMAND
    "${CMAKE_COMMAND}" -E make_directory "@HUNTER_PACKAGE_INSTALL_DIR@/lib"
    COMMAND
    "@HUNTER_SELF@/scripts/create-boost-ios-fatlib.sh"
    "@HUNTER_PACKAGE_SOURCE_DIR@"
    "${LIBNAME}"
    "@HUNTER_PACKAGE_COMPONENT@"
    "@HUNTER_PACKAGE_INSTALL_DIR@/lib"
    ${extra_lib_command}
)
